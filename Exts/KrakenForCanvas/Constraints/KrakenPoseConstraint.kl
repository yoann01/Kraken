require Kraken;

/// \dfgPresetColor Color(99, 129, 92)
struct KrakenPoseConstraint : KrakenConstraint {
};

/// \dfgPresetFolder Constraints
function KrakenPoseConstraint(
  Mat44 offset,
) {
  this.offset = offset;
}

/// \dfgPresetFolder Constraints
function KrakenPoseConstraint.addConstrainer!(
  Mat44 constrainer
) {
  this.constrainers.push(constrainer);
}

/// \dfgPresetFolder Constraints
function Mat44 KrakenPoseConstraint.compute?(
  Mat44 m
) {
  Xfo result = m;
  result.tr = Vec3();
  result.ori = Quat(0.0, 0.0, 0.0, 0.0);

  for(Size i=0;i<this.constrainers.size();i++) {
    Xfo constrainer(this.constrainers[i]);
    result.tr = result.tr.add(constrainer.tr);
    result.ori = result.ori.add(constrainer.ori);
    // todo: scaling is not maintained here!
  }

  result.ori.setUnit();
  result.tr *= 1.0 / Float32(this.constrainers.size());
  
  return result.toMat44() * this.offset;
}

/// \dfgPresetFolder Constraints
function Mat44 computeKrakenPoseConstraint(Mat44 offset, Mat44 constrainer, Mat44 constrainee) {
  KrakenPoseConstraint constraint(offset);
  constraint.addConstrainer(constrainer);
  return constraint.compute(constrainee);
}

/// \dfgPresetFolder Constraints
function Mat44 KrakenPoseConstraint.computeOffset?(
  Mat44 constrainee
) {

  KrakenPoseConstraint tmpConstraint(Mat44());
  tmpConstraint.constrainers = this.constrainers;

  Mat44 global = tmpConstraint.compute(constrainee);
  return global.inverse() * constrainee;
}

/// \dfgPresetFolder Constraints
function Mat44 KrakenPoseConstraint.computeOffsetSimple!(Mat44 constrainee, Mat44 constrainer) {
  this.offset = Mat44();
  this.constrainers.resize(1);
  this.constrainers[0] = constrainer;
  return this.computeOffset(constrainee);
}
